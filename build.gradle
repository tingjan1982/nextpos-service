plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.palantir.docker' version '0.22.1'

    // https://blog.jdriven.com/2017/01/spring-sweets-add-git-info-info-endpoint/
    id "com.gorylenko.gradle-git-properties" version "2.2.0"

    id "io.freefair.lombok" version "5.0.0-rc4"
}

group = 'io.nextpos'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.pkg.github.com/tingjan1982/einvoice-common'
        credentials {
            username project.findProperty("gpr.username") as String ?: System.getenv("GPR_USERNAME")
            password project.findProperty("gpr.password") as String ?: System.getenv("GPR_PASSWORD")
        }
    }
}

ext {
    set('springCloudVersion', "Greenwich.SR4")
}

dependencies {
    if (!project.hasProperty('ciBuild')) {
        implementation project(':einvoice-common')
    } else {
        implementation 'io.nextpos.einvoice:einvoice-common:1.4.3'
    }

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-jwt'
    implementation 'org.springframework.security:spring-security-oauth2-jose' // contains Jwt.Builder class
    implementation 'org.springframework.security:spring-security-oauth2-client' // contains ClientRegistration class
    implementation 'org.springframework.retry:spring-retry'

    //implementation 'com.hazelcast:hazelcast-spring'
    //implementation 'com.hazelcast:hazelcast-client'
    
    implementation 'org.apache.commons:commons-lang3'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'org.passay:passay:1.0' // password validation
    implementation 'com.twilio.sdk:twilio:7.41.1'
    implementation 'com.sendgrid:sendgrid-java:4.6.6'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.google.zxing:javase:3.3.0'
    implementation 'com.itextpdf:itext7-core:7.1.13'
    implementation 'org.apache.poi:poi:5.0.0'
    implementation 'org.apache.poi:poi-ooxml:5.0.0'

    //implementation 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.hsqldb:hsqldb'
    runtimeOnly 'com.google.cloud.sql:postgres-socket-factory:1.0.14'


    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    //annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'com.icegreen:greenmail:1.5.10'
    testImplementation 'org.awaitility:awaitility:3.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    
    /**
     * https://www.baeldung.com/spring-boot-embedded-mongodb
     */
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

/**
 * https://docs.gradle.org/current/userguide/java_testing.html#test_filtering
 */
test {
    useJUnitPlatform()
}

/**
 * Gradle Docker plugin: https://github.com/palantir/gradle-docker
 */
docker {
    dependsOn build
    name "joelin/${bootJar.archiveBaseName.get()}:latest"
    files bootJar.archiveFile.get()
    buildArgs(['JAR_FILE': "${bootJar.archiveFileName.get()}"])
}
